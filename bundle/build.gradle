plugins {
    id 'java'
    id 'application'
    // plugin for building the las2peer bundle jar
    id "com.github.johnrengelman.shadow" version "5.0.0"
    // maven plugin is used to create .pom files
    id 'maven-publish'
}

group = 'i5'
archivesBaseName = 'las2peer-bundle'
version = "${project.property('las2peer.version')}"
mainClassName = "i5.las2peer.tools.L2pNodeLauncher"
sourceCompatibility = "${project.property('java.version')}"
targetCompatibility = "${project.property('java.version')}"

repositories {
    // Use JCenter for resolving dependencies.
    jcenter()

    // DBIS Archiva
    maven {
        url "${project.property('dbis.archiva.url')}"
    }
}

dependencies {
    compile project(":core")
    compile project(":restmapper")
    compile project(":webconnector")
}

// put all .jar files into export/jars folder
tasks.withType(Jar) {
    destinationDir = file("$projectDir/export/jars")
}

jar {
    manifest {
        attributes "Main-Class": "i5.las2peer.tools.L2pNodeLauncher"
    }
}

// las2peer bundle jar
shadowJar {
    mergeServiceFiles() 
}

publishing {
    publications {
        bundle(MavenPublication) {
            pom {
                artifactId "las2peer-bundle"
                packaging "jar"
            }
        }
    }
}